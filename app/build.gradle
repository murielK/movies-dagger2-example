apply plugin: 'com.android.application'

def API_KEY
def localProperties = file('../local.properties')
if (localProperties.exists() && localProperties.canRead()) {
    Properties prop = new Properties()
    prop.load(new FileInputStream(localProperties))
    if (prop["API_KEY"] == null || prop["API_KEY"].isEmpty()) {
        throw new GradleException('Please add your api key in the local.properites file as follow: API_KEY="YOUR_API_KEY"')
    }
    API_KEY = prop['API_KEY']
} else throw new GradleException('local.properties file cannot be found or read, please create a local.properties file the project root and try again')


android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.murielkamgang.movies.dagger2"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "API_KEY", API_KEY)
        buildConfigField("String", "BASE_URL", '"https://api.themoviedb.org/3/"')
        buildConfigField("String", "BASE_POSTER_URL", '"https://image.tmdb.org/t/p/w342"')

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //Dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.22.1'
    implementation 'com.google.dagger:dagger-android:2.22.1'
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.22.1'

    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

}
